# Ch08 檔案管理


## 範例71 開啟以逗號分隔的文字檔案
```
Attribute VB_Name = "Module1"
Option Explicit
'----------------------------------
'範例71 開啟以逗號分隔的文字檔案
'----------------------------------

Sub OpenTxtFile()
    ChDrive ActiveWorkbook.Path
    ChDir ActiveWorkbook.Path
    
    Workbooks.OpenText FileName:="Fuji.txt", _
        DataType:=xlDelimited, Comma:=True
        
    MsgBox "開啟「Fuji.txt」。" & Chr(13) & _
        "若欲關閉，請執行巨集「CloseTxtFile」。"
End Sub

Sub CloseTxtFile()
    Workbooks("Fuji.txt").Close False
End Sub

```
## 範例72 開啟以逗號+空間分隔的檔案
```
'-----------------------------------------------
'範例72 開啟以逗號+空間分隔的檔案
'-----------------------------------------------

Sub OpenTxtFile2()
    ChDrive ActiveWorkbook.Path
    ChDir ActiveWorkbook.Path
    
    Workbooks.OpenText FileName:="Fuji2.txt", _
        DataType:=xlDelimited, ConsecutiveDelimiter:=True, _
        Comma:=True, Space:=True
        
        MsgBox "開啟「Fuji2.txt」。" & Chr(13) & _
            "若欲關閉，請執行巨集「CloseTxtFile2」。"
End Sub

Sub CloseTxtFile2()
    Workbooks("Fuji2.txt").Close False
End Sub

```
## 範例73 將數值資料轉換為文字
```
'-------------------------------
'範例73 將數值資料轉換為文字
'-------------------------------

Sub OpenTxtFile3()
    ChDrive ActiveWorkbook.Path
    ChDir ActiveWorkbook.Path
    
    Workbooks.OpenText FileName:="Fuji3.txt", _
        DataType:=xlDelimited, Comma:=True, _
        FieldInfo:=Array(Array(1, 2), Array(2, 1), Array(3, 2), _
            Array(4, 1), Array(5, 1), Array(6, 9), _
            Array(7, 2), Array(8, 2))
    
    MsgBox "開啟「Fuji3.txt」。" & Chr(13) & _
        "若欲關閉，請執行巨集「CloseTxtFile3」。"
End Sub

Sub CloseTxtFile3()
    Workbooks("Fuji3.txt").Close False
End Sub


```
## 範例74 開啟固定長度欄位的文字檔案
```
Attribute VB_Name = "Module2"
Option Explicit
'------------------------------------------------------
'範例74 開啟固定長度欄位的文字檔案
'------------------------------------------------------

Sub OpenTxtFile4()
    ChDrive ActiveWorkbook.Path
    ChDir ActiveWorkbook.Path
    
    Workbooks.OpenText FileName:="Nyukin.txt", DataType:=xlFixedWidth, _
        FieldInfo:=Array(Array(0, 2), Array(5, 5), Array(13, 1), _
            Array(17, 1), Array(36, 1))
    
    MsgBox "開啟「Nyukin.txt」。" & Chr(13) & _
        "若欲關閉，請執行巨集「CloseTxtFile4」。"
End Sub

Sub CloseTxtFile4()
    Workbooks("Nyukin.txt").Close False
End Sub



```
## 範例75 不開啟活頁簿而能讀入文字檔案
```
Attribute VB_Name = "Module1"
Option Explicit
Option Base 1
'----------------------------------------
'範例75 不開啟活頁簿而能讀入文字檔案
'----------------------------------------

Sub ReadTxt()
    Dim myTxtFile As String
    Dim myBuf(6) As String
    Dim i As Integer, j As Integer
    
    Application.ScreenUpdating = False
    
    myTxtFile = ActiveWorkbook.Path & "\Fuji.txt"
    
    Worksheets("郵遞區號").Activate
    
    Open myTxtFile For Input As #1
    
    Do Until EOF(1)
        Input #1, myBuf(1), myBuf(2), myBuf(3), myBuf(4), _
            myBuf(5), myBuf(6)
    
    '將資料置入儲存格內
        i = i + 1
        For j = 1 To 6
            Cells(i, j) = myBuf(j)
        Next j
    Loop
    
    Close #1
End Sub


```
## 範例76 輸出文件類型的文字檔案
```
'----------------------------------
'範例76 輸出文件類型的文字檔案
'----------------------------------

Sub ReadTxt2()
    Dim myTxtFile As String, myFNo As Integer, myBuf As String
    Dim i As Integer
       
    Application.ScreenUpdating = False
    
    myTxtFile = ActiveWorkbook.Path & "\WordPro.txt"
    
    Worksheets("文件類型").Activate
        
    myFNo = FreeFile            '取得可使用的檔案代碼
    Open myTxtFile For Input As #myFNo
    
    Do Until EOF(myFNo)
        Line Input #myFNo, myBuf
    
        i = i + 1
        Cells(i, 1) = myBuf    '將資料置入儲存格內
    Loop
    
    Close #myFNo
End Sub


```
## 範例77 將工作表的內容儲存為CSV格式檔案
```
'-----------------------------------
'範例77 將工作表的內容儲存為CSV格式檔案
'-----------------------------------

Sub WriteCsv()
    Dim myTxtFile As String, myFNo As Integer
    Dim myLastRow As Long, i As Long
       
    Application.ScreenUpdating = False
    
    myTxtFile = ActiveWorkbook.Path & "\Numazu.csv"
    
    Worksheets("郵遞區號2").Activate
    myLastRow = Range("A1").CurrentRegion.Rows.Count
    
    myFNo = FreeFile
    Open myTxtFile For Output As #myFNo
    
    For i = 1 To myLastRow
        Write #myFNo, Cells(i, 1), Cells(i, 2), Cells(i, 3), _
            Cells(i, 4), Cells(i, 5), Cells(i, 6)
    Next
    
    Close #myFNo
                
    MsgBox "「郵遞區號2」工作表內的資料將建立為「Numazu.csv」檔案。"
End Sub


```
## 範例78 將工作表的內容儲存為文件類型檔案
```
'-----------------------------------
'範例78 將工作表的內容儲存為文件類型檔案
'-----------------------------------

Sub WriteTxt()
    Dim myTxtFile As String, myFNo As Integer
    Dim myLastRow As Long, i As Long
       
    Worksheets("文件類型2").Activate
    Application.ScreenUpdating = False
    
    myTxtFile = ActiveWorkbook.Path & "\Column.txt"
    
    myLastRow = ActiveSheet.Cells.SpecialCells(xlCellTypeLastCell).Row
    
    myFNo = FreeFile
    Open myTxtFile For Output As #myFNo
    
    For i = 1 To myLastRow
        Print #myFNo, Cells(i, 1)
    Next
    
    Close #myFNo
                
    MsgBox "此工作表的資料將建立為為「Column.txt」檔案。"
End Sub




```
## 範例79 刪除資料夾內的檔案
```
Attribute VB_Name = "Module1"
Option Explicit
'----------------------------
'範例79 刪除資料夾內的檔案

Sub KillFile()
    Dim myPath As String
    
    myPath = ActiveWorkbook.Path & "\"
    
    If Dir(myPath & "DataBook.xls") <> "" Then
        Kill myPath & "DataBook.xls"
        
        MsgBox "DataBook.xls刪除完成，" & Chr(13) & _
            "若欲再次建立DataBook.xls，請執行巨集「MakeDataBook」。"
    Else
        MsgBox "找不到DataBook.xls"
    End If
End Sub

'建立DataBook.xls
Sub MakeDataBook()
    Dim myPath As String
    
    myPath = ActiveWorkbook.Path & "\"
    
    FileCopy myPath & "Dummy.xls", myPath & "DataBook.xls"
        
    MsgBox "DataBook.xls建立完成。"
End Sub


```
## 範例80 搜尋資料夾內符合條件的複數檔案
```
'--------------------------------
'範例80 搜尋資料夾內符合條件的複數檔案
'--------------------------------

Sub SearchFile()
    Dim myPath As String
    Dim myFName As String
    Dim i As Integer
    
    Worksheets("搜尋檔案").Activate
    i = 1
    Cells(i, 1).Value = "檔案名稱"
    Cells(i, 2).Value = "檔案大小"
    Cells(i, 3).Value = "檔案建立/修改日期"
    
    myPath = ActiveWorkbook.Path & "\"
    
    myFName = Dir(myPath & "*.xls")
    
    Do While myFName <> ""
        i = i + 1
        Cells(i, 1).Value = myFName
        Cells(i, 2).Value = FileLen(myPath & myFName)
        Cells(i, 3).Value = FileDateTime(myPath & myFName)
        
        myFName = Dir()
    Loop
End Sub

```
## 範例81 FileSearch的運用
```
'-------------------------------
'範例81 FileSearch的運用
'-------------------------------

Sub UseFileSearch()
    Dim myFSObj As FileSearch
    Dim i As Integer
    
    MsgBox "依序列出與8章-3.xls位在同資料夾的Excel活頁簿名稱。"
    
    Worksheets("搜尋檔案").Activate
            
    Set myFSObj = Application.FileSearch
    
    With myFSObj
        .LookIn = ActiveWorkbook.Path
        .Filename = "*.xls"
        
        If .Execute(SortBy:=msoSortByFileName, _
            SortOrder:=msoSortOrderAscending) > 0 Then
            
            MsgBox "搜尋到的Excel活頁簿數量為：" & .FoundFiles.Count & " 個。"
            
            For i = 1 To .FoundFiles.Count
                Cells(i, 1).Value = .FoundFiles(i)
            Next i
        
        Else
            MsgBox "找不到Excel活頁簿。"
        End If
    End With
End Sub



```
## 範例82 建立檔案後寫入資料(1)
```
Attribute VB_Name = "Module1"
Option Explicit
'--------------------------------------
'範例82 建立檔案後寫入資料(1)
'--------------------------------------

Sub FSOSample1()
    Dim myFSO As Object, myTS As Object
    
    Set myFSO = CreateObject("Scripting.FileSystemObject")
    Set myTS = myFSO.CreateTextFile("C:\FSOSample1.txt", True)
    
    myTS.WriteLine "本文翻譯日期：" & Date
    
    myTS.Close
End Sub


```
## 範例83 建立檔案後寫入資料(2)
```
'--------------------------------------
'範例83 建立檔案後寫入資料(2)
'--------------------------------------

Sub FSOSample2()
    Dim myFSO As New FileSystemObject
    
    With myFSO.CreateTextFile("C:\FSOSample1.txt", True)
    
        .WriteLine "本文翻譯日期：" & Date
    
        .Close
    End With
End Sub


```
## 範例84 檢查磁碟的容量
```
'--------------------------------------
'範例84 檢查磁碟的容量
'--------------------------------------

Sub FSOSample3()
    Dim myFSO As New FileSystemObject
    Dim myDS1 As Variant, myDS2 As Variant
    
    With myFSO.GetDrive("A")
        
        myDS1 = .TotalSize
        
        myDS2 = .AvailableSpace
    
    End With
    
    MsgBox "已使用空間：" & Format(myDS1 - myDS2, "#,##0") & vbCrLf & _
        "可用空間：" & Format(myDS2, "#,##0") & vbCrLf & vbCrLf & _
        "容量：" & Format(myDS1, "#,##0") & vbCrLf
End Sub


```
## 範例85 查詢磁碟的類型
```
'--------------------------------------
'範例85 查詢磁碟的類型
'--------------------------------------

Sub FSOSample4()
    Dim myFSO As New FileSystemObject
    Dim myDrv As Drive
    Dim myMsg As String
    
    For Each myDrv In myFSO.Drives
        
        myMsg = myMsg & myDrv.DriveLetter & "："
        
        Select Case myDrv.DriveType
            Case 0
                myMsg = myMsg & "不明" & vbCrLf
            Case 1
                myMsg = myMsg & "抽取式磁碟" & vbCrLf
            Case 2
                myMsg = myMsg & "硬式磁碟" & vbCrLf
            Case 3
                myMsg = myMsg & "網路磁碟" & vbCrLf
            Case 4
                myMsg = myMsg & "CD-ROM" & vbCrLf
            Case 5
                myMsg = myMsg & "RAM Disk" & vbCrLf
        End Select
    Next
    
    MsgBox myMsg
End Sub


```
## 範例86 檢查磁碟機的準備狀態
```
'--------------------------------------
'範例86 檢查磁碟機的準備狀態
'--------------------------------------

Sub FSOSample5()
    Dim myFSO As New FileSystemObject
    
    If myFSO.Drives("A").IsReady = True Then
        FileCopy "C:\Excel2003VBA應用篇\Fuji.txt", "A:\Fuji.txt"
    Else
        MsgBox "未插入磁碟片"
    End If
End Sub


```
## 範例87 取得子資料夾
```
'--------------------------------------
'範例87 取得子資料夾
'--------------------------------------

Sub FSOSample6()
    Dim myFSO As New FileSystemObject
    Dim myFld As Folder
    Dim i As Integer
    
    Worksheets("Sheet2").Activate
    i = 1
    
    With myFSO.GetFolder("C:\WINNT")
    
    '若發生錯誤，請將上行陳述式加上註記，然後執行下行陳述式
    'With myFSO.GetFolder("C:\Windows")
    
        For Each myFld In .SubFolders
        
            i = i + 1
            Cells(i, 1).Value = myFld.Name
            
        Next
    End With
End Sub


```
## 範例88 取得資料夾及其子資料夾內的檔案總容量
```
'------------------------------------------------------
'範例88 取得資料夾及其子資料夾內的檔案總容量
'------------------------------------------------------

Sub FSOSample7()
    Dim myFSO As New FileSystemObject
    Dim mySize1 As Variant, mySize2 As Variant
    
    With myFSO.GetFolder("C:\My Documents")
    
        mySize1 = .Size
        mySize2 = mySize1 / 1024 / 1024
        
        MsgBox _
            "C:\My Documents資料夾全部的檔案大小為：" & vbCrLf & vbCrLf & _
            Format(mySize2, "#,##0.0") & "MB" & " (" & _
            Format(mySize1, "#,##0") & "Byte)"
    
    End With
End Sub

```
## 08章-5 - Module1
```
Option Explicit

Sub myMacro1()
    MsgBox "執行巨集1", vbExclamation
End Sub
Sub myMacro2()
    MsgBox "執行巨集2", vbExclamation
End Sub

```
## 範例92 卸載增益集
```
'-----------------------------
'範例92 卸載增益集
'-----------------------------

Sub AddinUnInst()
    MsgBox "卸載增益集「08章-5」"
    AddIns("08章-5").Installed = False
End Sub



Attribute VB_Name = "Module2"
Option Explicit

```
## 範例91 載入增益集
```
'-------------------------
'範例91 載入增益集
'-------------------------

Sub AddinInst()
    If AddIns("08章-5").Installed = False Then
        MsgBox "載入增益集「08章-5」"
        AddIns("08章-5").Installed = True
    Else
        MsgBox "增益集「08章-5」已載入"
    End If
End Sub

```