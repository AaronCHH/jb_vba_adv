# Ch06 功能表列與工具列
```
Option Explicit

'--------------------------------------------------------
'變更儲存格的捷徑功能表
'--------------------------------------------------------

Private Sub Workbook_Open()
    Dim myCBCtrl As CommandBarButton
        
    Set myCBCtrl = Application.CommandBars("Cell").Controls.Add _
        (Type:=msoControlButton, Id:=872, Before:=8, Temporary:=True)
    myCBCtrl.Caption = "清除格式(&F)"
End Sub

'--------------------------------------------------------
'將變更過的捷徑功能表還原回預設值
'--------------------------------------------------------

Private Sub Workbook_BeforeClose(Cancel As Boolean)
    Application.CommandBars("Cell").Reset
End Sub
```

## 範例55 隱藏所有命令列
```
Attribute VB_Name = "Module1"
Option Explicit
'--------------------------------
'範例55 隱藏所有命令列
'--------------------------------

Sub UnVisibleAllCmdBars()
    Dim myCB As CommandBar

    On Error Resume Next        '若發生錯誤，程序繼續執行
    
    For Each myCB In Application.CommandBars
        myCB.Visible = False
    Next myCB
    
    On Error GoTo 0             '停用錯誤處理副程序
    
    Application.CommandBars("Worksheet Menu Bar").Enabled = False
    
    MsgBox "按[確定]將重新顯示被隱藏的所有命令列：" & Chr(13) & Chr(13) & _
        "‧工作表功能表列" & Chr(13) & _
        "‧[一般]工具列" & Chr(13) & _
        "‧[格式]工具列"
    
    With Application
        .CommandBars("Worksheet Menu Bar").Enabled = True
        .CommandBars("Standard").Visible = True
        .CommandBars("Formatting").Visible = True
    End With
End Sub





```
## 範例56 停用功能表
```
Option Explicit
'-----------------------
'範例56 停用功能表
'-----------------------

Private Sub Workbook_Open()
    Dim myCB As CommandBar, myCBCtrl As CommandBarControl
    
    Set myCB = Application.CommandBars("Worksheet Menu Bar")
    
    myCB.Controls("格式(&O)").Enabled = False
    myCB.Controls("工具(&T)").Controls("選項(&O)...").Enabled = False
End Sub
  
'回復功能表的狀態
Private Sub Workbook_BeforeClose(Cancel As Boolean)
    Application.CommandBars("Worksheet Menu Bar").Reset
End Sub



Option Explicit

'建立工作列
Sub AddCmdBar2()
    Dim myCB As CommandBar, myCBCtrl As CommandBarControl
    
    On Error Resume Next
    CommandBars("MyMacro4").Delete
    On Error GoTo 0
    
    Set myCB = Application.CommandBars.Add(Name:="MyMacro4")
       
    '建立切換按鈕
    Set myCBCtrl = myCB.Controls.Add(Type:=msoControlButton, Before:=1)
    
    With myCBCtrl
        .Caption = "ToggleBtn"
        .FaceId = 113
        .OnAction = "CBCtrlState1"
    End With
           
'-----建立[CheckMark]功能表-----
    Set myCBCtrl = myCB.Controls.Add(Type:=msoControlPopup, Before:=2)
    myCBCtrl.Caption = "CheckMark"
        
    '建立[CheckMark]→[CheckMarkOnOff]命令
    Set myCBCtrl = myCB.Controls("CheckMark").Controls _
        .Add(Type:=msoControlButton, Before:=1)
    myCBCtrl.Caption = "CheckMarkOnOff"
    myCBCtrl.OnAction = "CBCtrlState2"
        
'-----建立[刪除]功能表-----
    Set myCBCtrl = myCB.Controls.Add(Type:=msoControlPopup, Before:=3)
    myCBCtrl.Caption = "刪除(&D)"
    myCBCtrl.BeginGroup = True          '???????
    
    '建立[刪除]→[工具列]命令
    Set myCBCtrl = myCB.Controls("刪除(&D)").Controls _
        .Add(Type:=msoControlButton, Before:=1)
    myCBCtrl.Caption = "工具列(&T)"
    myCBCtrl.OnAction = "DeleteCmdBar"

    myCB.Visible = True

    MsgBox "按下按鈕時會切換開/關的狀態，" & Chr(13) & _
        "若點功能表，也會切換開/關的狀態。" & Chr(13) & _
         Chr(13) & "若需刪除此工具列，" & Chr(13) & _
        "請點[刪除]→[工具列]。"
End Sub


```
## 範例57 切換按鈕的狀態
```
'-------------------------------------------
'範例57 切換按鈕的狀態
'-------------------------------------------

Private Sub CBCtrlState1()
    Dim myCBCtrl As CommandBarButton
    
    Set myCBCtrl = CommandBars("MyMacro4").Controls("ToggleBtn")
    
    If myCBCtrl.State = msoButtonDown Then
         myCBCtrl.State = msoButtonUp
         'myCBCtrl.State = msoButtonMixed
         MsgBox "按鈕的狀態為「關」"
    Else
         myCBCtrl.State = msoButtonDown
         MsgBox "按鈕的狀態為「開」"
      End If
End Sub

```
## 範例57 切換功能表的狀態
```
'---------------------------------------------
'範例57 切換功能表的狀態
'---------------------------------------------

Private Sub CBCtrlState2()
    Dim myCBCtrl As CommandBarButton
    
    Set myCBCtrl = CommandBars("MyMacro4").Controls("CheckMark") _
        .Controls("CheckMarkOnOff")
    
    If myCBCtrl.State = msoButtonDown Then
         myCBCtrl.State = msoButtonUp
         'myCBCtrl.State = msoButtonMixed
         MsgBox "Check Mark的狀態為「關」"
    Else
         myCBCtrl.State = msoButtonDown
         MsgBox "Check Mark的狀態為「開」"
      End If
End Sub


'刪除工具列
Private Sub DeleteCmdBar()
    MsgBox "刪除工具列「MyMacro4」"
    Application.CommandBars("MyMacro4").Delete
End Sub
```