# Ch09 與其他應用程式的共同運作


## 範例93 使用早期繫結控制Word物件
```
Attribute VB_Name = "Module1"
Option Explicit
    Dim objWord As New Word.Application     'Word應用程式
    Dim objWordDoc As Word.Document         '新增的Word文件
'-----------------------------------------------------------
'範例93 使用早期繫結控制Word物件
'-----------------------------------------------------------

Sub CreateWordApp()
    Dim myPath As String
    Dim myReturn As Integer
    
    myPath = ActiveWorkbook.Path & "\"
    
    myReturn = MsgBox(prompt:="「Test.doc」是關閉的嗎？" & _
        "(若是開啟的，將發生錯誤)", Buttons:=vbYesNo)
    If myReturn = vbNo Then Exit Sub
    
    With objWord
        .Visible = True                                               '顯示Word
        .WindowState = wdWindowStateMaximize '將視窗最大化
        .Documents.Add                                            '新增文件
        
        '將新增文件代入物件變數中
        Set objWordDoc = .ActiveDocument
    End With
    
    '在文件中插入文字
    With objWord.Selection
        .InsertAfter "建立Word物件的測試"
        .InsertParagraphAfter
        .InsertAfter Now() & " 建立"
        .MoveRight
    End With
    
    '設定段落1的格式
    With objWordDoc.Paragraphs(1).Range
        .ParagraphFormat.Alignment = wdAlignParagraphCenter
        With .Font
            .Name = "標楷體"
            .Size = 20
            .Bold = True
        End With
    End With
    
    '設定段落2的格式
    objWordDoc.Paragraphs(2).Range.ParagraphFormat _
        .Alignment = wdAlignParagraphRight
    
    Application.Wait Now() + TimeValue("00:00:03")
    
    objWord.WindowState = wdWindowStateMinimize '將視窗最小化

    MsgBox "啟動Word並建立新文件" & Chr(13) & _
        "按下[確定]按鈕後儲存文件並關閉Word"

    objWordDoc.SaveAs myPath & "Test.doc"   '將新增的文件另存新檔
    objWordDoc.Close                        '關閉新增的文件
    objWord.Quit                            '關閉Word

    Set objWord = Nothing                   '清除物件變數的內容
    Set objWordDoc = Nothing
End Sub


```
## 範例94 將Excel工作表傳送到Word文件中
```
'-----------------------------------------------------------
'範例94 將Excel工作表傳送到Word文件中
'-----------------------------------------------------------

Sub CreateWordApp2()
    
    With objWord
        .Visible = True                      '顯示Word
        .WindowState = wdWindowStateMaximize '將視窗最大化
        .Documents.Open ActiveWorkbook.Path & "\Report.doc" '開啟Report.doc
        
        '將Report.doc代入物件變數中
        Set objWordDoc = .ActiveDocument
        
        '在文件中插入文字
        With .Selection
            .Move Count:=objWordDoc.Characters.Count
            .InsertParagraphAfter
            .InsertAfter "CD販售張數"
            .InsertParagraphAfter
            .MoveRight
        End With
    End With
    
    '複製儲存格的資料
    Worksheets("CD販售量").Range("CD販賣張數").Copy
    
    '貼到Word內
    With objWord.Selection
        .Paste
        .TypeParagraph
    End With
    
    '複製圖表
    Worksheets("CD販售量").ChartObjects(1).Copy
    
    '設定貼到Word的格式
    With objWord
        .Selection.PasteSpecial Placement:=wdInLine, _
            DataType:=wdPasteMetafilePicture
        .Selection.ParagraphFormat.Alignment = _
            wdAlignParagraphCenter
    End With
    
    '列印(列印時將中斷巨集的執行)
    objWord.PrintOut Background:=False
    
    '不儲存文件，關閉Word
    objWordDoc.Close SaveChanges:=False
    
    objWord.Quit                '關閉Word
    
    Set objWord = Nothing       '清除物件變數的內容
    Set objWordDoc = Nothing
End Sub


```
## 範例95 存取已啟動的Word
```
'----------------------------------
'範例95 存取已啟動的Word
'----------------------------------

Sub GetWordApp()
    On Error GoTo HandleErr
    Dim myAppOpen As Boolean
    
    Set objWord = GetObject(, "Word.Application")
    myAppOpen = True
    
MacroContinue:
    If myAppOpen = False Then
        Set objWord = CreateObject("Word.Application")
    End If
    
    With objWord
        .Visible = True
        .WindowState = wdWindowStateMinimize
        .Documents.Add
    End With
    
    Set objWord = Nothing
    
    Exit Sub

HandleErr:
    '若尚未將ActiveX元件建立為物件
    If Err.Number = 429 Then
        myAppOpen = False
        Resume MacroContinue
    End If
End Sub




```
## 範例96 控制記事本
```
Attribute VB_Name = "Module2"
Option Explicit  
'------------------------------
'範例96 控制記事本
'------------------------------

Sub ControlNotePad()
    Dim myPath As String
    Dim myID As Double             'Shell函數的傳回值

    myPath = ActiveWorkbook.Path & "\"

    '開啟記事本
    myID = Shell("Notepad.exe", vbNormalFocus)
    
'<若無法開啟記事本，請加上記事本程式的路徑>
    'myID = Shell("C:\Windows\Notepad.exe", vbNormalFocus)

    '由[Alt]+[F]+[O]鍵傳送開啟[開啟舊檔]對話方塊的命令
    SendKeys "%FO", True

    '指定並傳送開啟檔案名稱的按鍵代碼
    SendKeys myPath & "Report.txt", True

    '由[ENTER]鍵傳送開啟檔案的命令
    SendKeys "{ENTER}", True

    '複製儲存格的內容
    Worksheets("書籍販售量").Range("書籍販售量預估").Copy
    
    '開啟記事本
    AppActivate myID
    
    '由[Ctrl]+{V]鍵傳送貼上複製內容的命令
    SendKeys "^V", True
    
    '解除複製模式
    Application.CutCopyMode = False
End Sub



```
## 範例97 關閉以Shell函數啟動的應用程式之前進入待機狀態
```
Attribute VB_Name = "Module3"
Option Explicit
'-------------------------------------------------------
'範例97 關閉以Shell函數啟動的應用程式之前進入待機狀態
'-------------------------------------------------------

'傳回既存物件控制代碼的函數
'傳回值　成功 = 指定處理的Open控制代碼
' 　　　　失敗 = NULL
Declare Function OpenProcess Lib "kernel32" _
    (ByVal dwDesiredAccess As Long, _
    ByVal bInheritHandle As Long, _
    ByVal dwProcessId As Long) As Long

Public Const PROCESS_QUERY_INFORMATION = &H400&


'傳回指定處理結束狀態的函數
'傳回值　成功 = 0以外的數值
'　　　　失敗 = 0
Declare Function GetExitCodeProcess Lib "kernel32" _
    (ByVal hProcess As Long, _
    lpExitCode As Long) As Long
        
'判斷指定的處理是否結束
'(若尚未結束，將置入STILL_ACTIVE)
Public Const STATUS_PENDING = &H103&
Public Const STILL_ACTIVE = STATUS_PENDING


'關閉開啟中物件控制代碼的函數
''傳回值　成功 = 0以外的數值
'　　　　失敗 = 0
Declare Function CloseHandle Lib "kernel32" _
    (ByVal hObject As Long) As Long


'程序
Sub GetExitCodeProcess_Sample()
    Dim lngProcessId As Long    'Shell函數的傳回值
    Dim lngProcess As Long      'OpenProcess函數的傳回值
    Dim lngExitCode As Long     '結束程式碼
    Dim rc As Long
     
    MsgBox "按[確定]按鈕後將開啟記事本，關閉記事本之前程序將處於暫停的狀態"
              
    '啟動記事本
    lngProcessId = Shell("Notepad.exe", vbNormalFocus)
    
    '取得以Shell函數啟動的應用程式的處理物件的控制代碼
    lngProcess = OpenProcess(PROCESS_QUERY_INFORMATION, _
                            1, _
                            lngProcessId)
                            
    '以GetExitCodeProcess取得處理的結束狀態
    '若啟動的應用程式處於尚未關閉的狀態，將由DoEvent繼續對作業系統詢問其狀態
    Do
        rc = GetExitCodeProcess(lngProcess, lngExitCode)
        DoEvents
    Loop While lngExitCode = STILL_ACTIVE

    '關閉開啟中的物件控制代碼
    rc = CloseHandle(lngProcess)

    MsgBox "記事本關閉"
End Sub




```
## 範例98 避免重複啟動應用程式
```
Attribute VB_Name = "Module4"
Option Explicit
'-------------------------------------------------------
'範例98 避免重複啟動應用程式
'-------------------------------------------------------


'從Class名稱或Caption取得視窗控制代碼的函數
'傳回值　成功 = 視窗控制代碼
' 　　　　失敗 = NULL
Declare Function FindWindow Lib "user32" _
    Alias "FindWindowA" _
    (ByVal lpClassName As String, _
    ByVal lpWindowName As String) As Long

'程序
Sub FindWindow_Sample()
    Dim strClassName As String  'Class名稱
    Dim rc As Long
    
    Dim lngProcessId As Long    'Shell函數的傳回值
    
    '指定Class名稱
    strClassName = "SciCalc"
        
    '取得小算盤的視窗控制代碼
    rc = FindWindow(strClassName, _
                    vbNullString)
                    
    '取得視窗控制代碼但尚未啟動
    If rc <> 0& Then
        MsgBox "小算盤已開啟"
        Exit Sub
    End If
        
    '啟動小算盤
    lngProcessId = Shell("Calc.exe", vbNormalFocus)
End Sub
```