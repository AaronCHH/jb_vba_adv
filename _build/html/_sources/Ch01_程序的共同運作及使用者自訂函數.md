# Ch01 程序的共同運作及使用者自訂函數
## 01章-1

## 範例1 從主程序呼叫副程序
```
Attribute VB_Name = "Module1"
'-----------------------------------------
'範例1 從主程序呼叫副程序
'(排序→篩選→列印→停用自動篩選)
'-----------------------------------------

Sub PrintMember()
    SortData        '副程序(排序)
    
    SelectData      '副程序(篩選)
    
    PrintData       '副程序(列印)
    
    KaijoFilter     '副程序(停用自動篩選)
End Sub

'-----------
'副程序
'-----------

'1.排序
Sub SortData()
    Range("A1").Sort Key1:=Range("D3"), Order1:=xlAscending, _
        Key2:=Range("F3"), Order2:=xlAscending, Header:=xlGuess
End Sub

'2.篩選
Sub SelectData()
    Range("A1").AutoFilter Field:=9, Criteria1:=">=2003/1/1", _
        Operator:=xlAnd, Criteria2:="<=2006/12/31"
End Sub

'3.列印資料
Sub PrintData()
    Range("A1").CurrentRegion.Offset(1).Select
    Selection.Resize(Selection.Rows.Count - 1).Select
    ActiveSheet.PageSetup.PrintArea = Selection.Address
    ActiveSheet.PrintOut
End Sub

'4.停用自動篩選
Sub KaijoFilter()
    Range("A1").Select
    Selection.AutoFilter
End Sub


```
## 範例2 呼叫使用引數的副程序
```
Attribute VB_Name = "Module2"
'-------------------------------
'範例2 呼叫使用引數的副程序
'-------------------------------

Sub SortMember()
    Dim myRowNo1 As Integer, myRowNo2 As Integer

    Worksheets("會員名冊").AutoFilterMode = False
    
    myRowNo1 = Application.InputBox("請輸入欲排序的行數(第幾行)", _
        "此為第一順位")
    
    If myRowNo1 < 1 Or myRowNo1 > 10 Then Exit Sub
    
    myRowNo2 = Application.InputBox("請輸入欲排序的行數(第幾行)", _
        "此為第二順位")
    
    If myRowNo2 < 1 Or myRowNo2 > 10 Then Exit Sub
        
    RunSort myRowNo1, myRowNo2
End Sub

Sub RunSort(myR1 As Integer, myR2 As Integer)
    Range("A1").Sort Key1:=Cells(3, myR1), Order1:=xlAscending, _
        Key2:=Cells(3, myR2), Order2:=xlAscending, Header:=xlGuess

    'MsgBox myR1
    'MsgBox myR2
End Sub
```
## 01章-2

## 範例3 使用引用呼叫其他活頁簿的程序
```
Attribute VB_Name = "Module1"
Option Explicit
'---------------------------------------------
'範例3 使用引用呼叫其他活頁簿的程序
'---------------------------------------------

Sub SansyoSettei()
    Call Sample1
End Sub
```
## 範例4 使用Run方法呼叫其他活頁簿的程序
```
'------------------------------------------------
'範例4 使用Run方法呼叫其他活頁簿的程序
'(不指定路徑)
'------------------------------------------------

Sub AppRun()
    Application.Run "Subrtin2.xls!Sample2"
End Sub

```
## 範例5 使用指定的路徑呼叫其他活頁簿的程序
```
'-------------------------------------------------
'範例5 使用指定的路徑呼叫其他活頁簿的程序
'-------------------------------------------------

Sub AppRun2()
    Dim myWBPath As String
    
    myWBPath = ActiveWorkbook.Path
    
    Application.Run "'" & myWBPath & "\Subrtin2.xls'!Sample2"
End Sub

```

## 01章-3

## 範例6 以傳址的方式將引數傳送到副程序
```
'--------------------------------
'範例6 以傳址的方式將引數傳送到副程序
'--------------------------------
Attribute VB_Name = "Module1"
Option Explicit

Sub SansyoWatashi()
    Dim myNumber As Integer
    
    myNumber = 1
    
    ChangeNumber1 myNumber
    
    MsgBox myNumber
End Sub

Sub ChangeNumber1(ByRef n As Integer)
     
    n = 2

End Sub
```

## 範例7 以傳值的方式將引數傳送到副程序
```
'--------------------------------
'範例7 以傳值的方式將引數傳送到副程序
'--------------------------------

Sub AtaiWatashi()
    Dim myNumber As Integer
    
    myNumber = 1
    
    ChangeNumber2 myNumber
    
    MsgBox myNumber
End Sub

Sub ChangeNumber2(ByVal n As Integer)
     
    n = 2

End Sub

```

## 01章-4

## 範例8 建立Function程序
```
'-----------------------------
'範例8 建立Function程序
'-----------------------------
Attribute VB_Name = "Module1"
Option Explicit

Sub TestResult()
    Range("A1").Select
    
    MsgBox TestMsg
End Sub

Function TestMsg() As String
    Select Case ActiveCell.Value
        Case Is > 80
            TestMsg = "優"
        Case Is > 60
            TestMsg = "良"
        Case Is > 40
            TestMsg = "不及格"
        Case Else
            TestMsg = "需努力"
    End Select
End Function


```
## 範例9 單一引數的使用者自訂函數
```
Attribute VB_Name = "Module2"
Option Explicit
'-----------------------------
'範例9 單一引數的使用者自訂函數
'-----------------------------

Function MyMsg(Result As Variant) As String
    Select Case Result
        Case Is > 80
            MyMsg = "優"
        Case Is > 60
            MyMsg = "良"
        Case Is > 40
            MyMsg = "不及格"
        Case Else
            MyMsg = "需努力"
    End Select
End Function

```
## 範例10 複數引數的使用者自訂函數
```
'-----------------------------
'範例10 複數引數的使用者自訂函數
'-----------------------------

Function MyFindS(r As Range, s As String) As Long
    Dim i As Long           '符合搜尋結果的儲存格數
    Dim myCell As Range

    For Each myCell In r
        If InStr(myCell.Value, s) > 0 Then
            i = i + 1
        End If
    Next
    
    MyFindS = i
End Function

```
## 範例11 加總指定儲存格範圍數值的使用者自訂函數
```
'----------------------------------------------
'範例11 加總指定儲存格範圍數值的使用者自訂函數
'----------------------------------------------

Function MySum(r As Range) As Double
    Dim myCell As Range

    For Each myCell In r
        MySum = Val(myCell.Value) + MySum
    Next
End Function

```
## 範例12 自動重覆運算的使用者自訂函數
```
'----------------------------------------------
'範例12 自動重覆運算的使用者自訂函數
'----------------------------------------------

Function MyKaihi(n As Integer) As Long
    Application.Volatile
    MyKaihi = Range("A2").Value * n
End Function
```